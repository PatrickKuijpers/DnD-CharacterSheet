apply plugin: 'com.android.application'
apply plugin: 'com.getkeepsafe.dexcount'

// Load keystore
def keystorePropertiesFile = file("D:\\Keystore\\keystore.properties");
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    compileSdkVersion 25
    buildToolsVersion "23.0.2"

    defaultConfig {
        applicationId "nl.tcilegnar.dndcharactersheet"
        minSdkVersion 16
        targetSdkVersion 25
        versionCode 12
        versionName "0.3.4"
        multiDexEnabled true
    }
    signingConfigs {
        release {
            storeFile file(keystoreProperties['signing.store.file'])
            storePassword keystoreProperties['signing.store.password']
            keyAlias keystoreProperties['signing.key.alias.DnD']
            keyPassword keystoreProperties['signing.key.password.DnD']
        }
        debug {
            storeFile file('../debug.keystore')
        }
    }
    buildTypes {
        release {
            signingConfig signingConfigs.release
            // https://developer.android.com/studio/build/shrink-code.html
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            signingConfig signingConfigs.debug
            applicationIdSuffix ".dev"
            versionNameSuffix ".dev"
        }
        alpha {
            initWith debug
            applicationIdSuffix ".test"
            versionNameSuffix ".test"
        }
    }
    productFlavors {
        DnD3x5 {
            applicationIdSuffix ".version_three_five"
        }
        DnD4x0 {
            applicationIdSuffix ".version_four_zero"
        }
        DnD5x0 {
            applicationIdSuffix ".version_five_zero"
        }
    }

    // Voorkom dat errors van lint builds breken in Jenkins
    lintOptions {
        abortOnError false
    }

    /*
    aaptOptions {
        cruncherEnabled false
    }
    testOptions {
        unitTests.all {
            jacoco {
                includeNoLocationClasses true
            }
        }
    }
    */

    dexOptions {
        jumboMode true
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile 'com.android.support:appcompat-v7:25.1.0'
    compile 'com.android.support:design:25.1.0'
    compile 'com.android.support:multidex:1.0.1'
    compile 'net.hockeyapp.android:HockeySDK:4.0.0'
    testCompile 'junit:junit:4.12'
    testCompile 'org.robolectric:robolectric:3.1'
    testCompile 'org.mockito:mockito-all:1.9.5'
}

dexcount {
    format = "list"
    includeClasses = true
    includeFieldCount = true
    includeTotalMethodCount = true
    orderByMethodCount = true
    verbose = true
    maxTreeDepth = Integer.MAX_VALUE
    teamCityIntegration = false
    enableForInstantRun = false
    teamCitySlug = null
    runOnEachAssemble = true
}
